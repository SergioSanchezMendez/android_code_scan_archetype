// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        versionCode = 1
        versionName = "1.0"
        projectAppCompatVersion     = 'com.android.support:appcompat-v7:25.3.1'
        projectSupportVersion       = 'com.android.support:support-v4:25.3.1'
        projectDesignSupportVersion = 'com.android.support:design:25.3.1'
        projectZXingCoreVersion     = "com.google.zxing:core:3.3.0"
        projectCompileSdkVersion    = 26
        projectMinSdkVersion        = 14
        projectTargetSdkVersion     = 26
        projectBuildToolsVersion    = "26.0.2"
    }

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}

allprojects {
    group = 'safon.tarot4all'
    version = '0.0.1'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    ext {
        isLibrary = false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion 26
                buildToolsVersion "26.0.2"

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion 14
                    targetSdkVersion 26
                    versionCode 1
                    versionName "0.0.1"
                }
            }
        }
        if(project.isLibrary && rootProject.hasProperty('sonatypeUsername') && rootProject.hasProperty('sonatypePassword')) {
            configure(project) {
                apply plugin: 'maven'
                apply plugin: 'signing'

                signing {
                    required { gradle.taskGraph.hasTask("uploadArchives") }
                    sign configurations.archives
                }

                uploadArchives {
                    repositories.mavenDeployer {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        repository(url: sonatypeRepo) {
                            authentication(userName: sonatypeUsername , password: sonatypePassword)
                            releases(updatePolicy: 'always')
                        }

                        snapshotRepository(url: sonatypeSnapshotRepo) {
                            authentication(userName: sonatypeUsername , password: sonatypePassword)
                            snapshots(updatePolicy: 'always')
                        }

                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/X'

                            scm {
                                url 'scm:git@github.com:X/barcodescanner.git'
                                connection 'scm:git@github.com:X/barcodescanner.git'
                                developerConnection 'scm:git@github.com:X/barcodescanner.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'X'
                                    name 'XXX'
                                    email 'X@gmail.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}